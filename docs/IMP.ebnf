Letter = 'a' | 'b' | 'c' | 'd' | 'e' | 'f' | 'g' | 'h' | 'i' | 'j' | 'k' | 'l' | 'm' | 'n' | 'o' | 'p' | 'q' | 'r' | 's' | 't' | 'u' | 'v' | 'w' | 'x' | 'y' | 'z' | 'A' | 'B' | 'C' | 'D' | 'E' | 'F' | 'G' | 'H' | 'I' | 'J' | 'K' | 'L' | 'M' | 'N' | 'O' | 'P' | 'Q' | 'R' | 'S' | 'T' | 'U' | 'V' | 'W' | 'X' | 'Y' | 'Z'
Digit = '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' | '9'
Ident = Letter { Letter | Digit }                                                   (* alphanumeric identifier *)
Var = Ident | '_'                                                                   (* variable or placeholder *)
Val = [ '-' ] Digit { Digit }                                                       (* unbounded integer value *)
Bool = 'true' | 'false'                                                             (* boolean value *)

Aop = '+' | '-' | '*' | '/' | '%'                                                   (* arithmetic operation *)
Aexp = Aterm { Aop Aterm }                                                          (* arithmetic expression *)
Aterm
    = Ident                                                                         (* variable *)
    | Val                                                                           (* value *)
    | 'time' Stm                                                                    (* time statement *)
    | '(' Aexp ')'                                                                  (* parenthesized expression *)

Rop = '=' | '#' | '<' | '<=' | '>' | '>='                                           (* relation operation *)
Bop = 'and' | 'or'                                                                  (* boolean operation *)
Bexp = Bterm { Bop Bterm }                                                          (* boolean expression *)
Bterm
    = Bool                                                                          (* literal *)
    | 'not' Bexp                                                                    (* negation *)
    | Aexp Rop Aexp                                                                 (* relation *)
    | '(' Bexp ')'                                                                  (* parenthesized expression *)

Dop = ':=' | '+=' | '-=' | '*=' | '/=' | '%='                                       (* definition operator *)
Stm
    = 'skip'                                                                        (* no-op *)
    | Var Dop Aexp                                                                  (* variable definition *)
    | Stm ';' Stm                                                                   (* sequence *)
    | 'if' Bexp 'then' Stm 'else' Stm 'end'                                         (* conditional *)
    | 'while' Bexp 'do' Stm 'end'                                                   (* pre-exec conditional iteration *)
    | 'print' Aexp                                                                  (* print arithmetic expression *)
    | 'read' Var                                                                    (* read input into variable *)
    | 'var' Ident Dop Aexp 'in' Stm 'end'                                           (* local variable definition *)
    | Stm 'par' Stm                                                                 (* parallelization *)
    | Stm '[]' Stm                                                                  (* non-determinism *)
    | 'procedure' Ident '(' Params ';' Rets ')' 'begin' Stm 'end'                   (* procedure definition *)
    | Ident '(' Args ';' Rets ')'                                                   (* procedure invocation *)
    | 'repeat' Stm 'until' Bexp                                                     (* post-exec conditional iteration *)
    | 'for' Ident ':=' Aexp 'to' Aexp 'do' Stm 'end'                                (* incremental iteration *)
    | 'do' Aexp 'times' Stm                                                         (* arithmetic iteration *)
    | 'revert' Stm 'if' Bexp                                                        (* conditional revert *)
    | 'break'                                                                       (* break flag *)
    | 'match' Aexp 'on' { Val ':' Stm ',' } 'default:' Stm                          (* pattern match *)
    | 'havoc' Var                                                                   (* random variable definition *)
    | 'assert' Bexp                                                                 (* assertion *)
    | 'flip' '(' Val ')' Stm 'flop' Stm 'end'                                       (* alternating branching *)
    | 'raise' Aexp                                                                  (* raise exception *)
    | 'try' Stm 'catch' Ident 'with' Stm 'end'                                      (* exception handling *)
    | 'swap' Ident Ident                                                            (* swap assign two variables *)
    | Stm 'alternate' Stm                                                           (* alternating parallelization *)
    | '(' Stm ')'                                                                   (* parenthesized statement *)

Args = [ Aexp { ',' Aexp } ]                                                        (* zero or more comma separated Aexp *)
Params = [ Ident { ',' Ident } ]                                                    (* '' .. Ident *)
Rets = [ Var { ',' Var } ]                                                          (* '' .. Var *)
