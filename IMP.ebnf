Letter = 'a' | .. | 'z' | 'A' | .. | 'Z'
Digit = '0' | .. | '9'
Ident = Letter { Letter | Digit }                                                   (* alphanumeric identifier *)
Var = Ident | '_'                                                                   (* variable or placeholder *)
Numeral = [ '-' ] ( Digit | Numeral Digit )                                         (* unbounded integer value *)
Bool = 'true' | 'false'                                                             (* boolean value *)

Aop = '+' | '-' | '*' | '/' | '%'                                                   (* arithmetic operation *)
Aexp = Aterm { Aop Term }                                                           (* arithmetic expression *)
Aterm
    = Ident                                                                         (* variable *)
    | Numeral                                                                       (* literal *)
    | 'time' Stm                                                                    (* time statement *)
    | '(' Aexp ')'                                                                  (* parenthesized expression *)

Rop = '=' | '#' | '<' | '<=' | '>' | '>='                                           (* relation operation *)
Bop = 'and' | 'or'                                                                  (* boolean operation *)
Bexp = Bterm { Bop Bterm }                                                          (* boolean expression *)
Bterm
    = Bool                                                                          (* literal *)
    | 'not' Bexp                                                                    (* negation *)
    | Aexp Rop Aexp                                                                 (* relation *)
    | '(' Bexp ')'                                                                  (* parenthesized expression *)

Dop = ':=' | '+=' | '-=' | '*=' | '/=' | '%='                                       (* definition operator *)
Stm
    = 'skip'                                                                        (* no-op *)
    | Var Dop Aexp                                                                  (* variable definition *)
    | Stm ';' Stm                                                                   (* sequence *)
    | 'if' Bexp 'then' Stm 'else' Stm 'end'                                         (* conditional *)
    | 'while' Bexp 'do' Stm 'end'                                                   (* pre-execution boolean evaluation iteration *)
    | 'print' Aexp                                                                  (* print arithmetic expression *)
    | 'read' Var                                                                    (* read input into variable *)
    | 'var' Ident Dop Aexp 'in' Stm 'end'                                           (* local variable definition *)
    | Stm 'par' Stm                                                                 (* parallelization *)
    | Stm '[]' Stm                                                                  (* non-determinism *)
    | 'procedure' Ident '(' Params ';' Rets ')' 'begin' Stm 'end'                   (* procedure definition *)
    | Ident '(' Args ';' Rets ')'                                                   (* procedure invocation *)
    | 'repeat' Stm 'until' Bexp                                                     (* post-execution boolean evaluation iteration *)
    | 'for' Ident ':=' Aexp 'to' Aexp 'do' Stm 'end'                                (* incremental iteration *)
    | 'do' Aexp 'times' Stm                                                         (* arithmetic iteration *)
    | 'revert' Stm 'if' Bexp                                                        (* conditional revert *)
    | 'break'                                                                       (* break flag *)
    | 'match' Aexp 'on' { Numeral ':' Stm ',' } 'default:' Stm                      (* pattern match *)
    | 'havoc' x                                                                     (* random variable definition *)
    | 'assert' Bexp                                                                 (* assertion *)
    | 'flip' '(' Numeral ')' Stm 'flop' Stm 'end'                                   (* alternating branching *)
    | 'raise' Aexp                                                                  (* raise exception *)
    | 'try' Stm 'catch' Ident 'with' Stm 'end'                                      (* exception handling *)
    | 'swap' Ident Ident                                                            (* swap assign two variables *)
    | '(' Stm ')'                                                                   (* parenthesized statement *)

Args = [ Aexp { ',' Aexp } ]                                                        (* zero or more comma separated Aexp *)
Params = [ Ident { ',' Ident } ]                                                    (* '' .. Ident *)
Rets = [ Var { ',' Var } ]                                                          (* '' .. Var *)
